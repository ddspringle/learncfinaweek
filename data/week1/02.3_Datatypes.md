In our last Section, we talked about variables, data, and how to
transport data around in your application. Different data types serve
different purposes.

## Strings/Numbers

**Is Simple:** Yes

Strings and numbers are very easy to work with. To set a string or a
number, use the cfset command. To append strings and numbers to each
other, use the & operator:

```cfml
<cfset aString = "hi">
<cfset aNumber = 42>
<cfset aStringAndANumber = aString & aNumber>

aString: <cfoutput>#aString#</cfoutput>
aNumber: <cfoutput>#aNumber#</cfoutput>
aStringAndANumber: <cfoutput>#aStringAndANumber#</cfoutput>
```

If you have a big block of strings to set, you can use the cfsavecontent
command.

```cfml
<cfsavecontent variable="EmailContent">
    Hi

    We want to send you a hoverboard.
    Let us know if you will accept this free offer.

    -Us
</cfsavecontent>

<cfoutput>#EmailContent#</cfoutput>
```

## Dates

**Is Simple:** Kind of

Dates are also very easy to work with in ColdFusion. You can use built
in functions like now() to make a date, or you can type the date into
the variable assignment like this:

```cfml
<cfset DateToday = now()>
<cfset NewYearDay = "1/1/2019">
```

You can use built-in functions to work with dates. To show how many days
it has been since the turn of the century:

```cfml
<cfset DaysSinceTurnOfCentury = DateDiff("d", "1/1/2000", now() )>
<cfoutput>#DaysSinceTurnOfCentury#</cfoutput>
```

Or suppose you want to know what the date will be 42 days from now:

```cfml
<cfset FortyTwoDaysFromNow = DateAdd("d", now(), 42 )>
```

ColdFusion provides many useful functions to work with dates and times.
You can compare dates, find out how far two dates are apart, reformat a
date, and more. See the ColdFusion Date/Time documentation for a full
listing:
[https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/date-and-time-functions.html](https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/date-and-time-functions.html)

## Arrays

**Is Simple:** No

Arrays are an ordered series of data. Here's an example of a one
dimensional array:

### Array Creation

```cfml
<cfset ThingsILike = ["Warm Sandy Beaches", "Tropical Drinks", 42]>
<cfdump var = "#ThingsILike#">
```

### Alternate Method: Array Creation

Here's another way to create an array, along with a couple of different
ways to add data to an array:

```cfml
<cfset ThingsILike = arrayNew(1)>
```

### Adding Items to an Array

You can add things by a specific position. Note: Arrays in ColdFusion
start at 1, not 0.

```cfml
<cfset ThingsILike[1]  = "Warm Sandy Beaches">
```

### Alternate Method: Adding Items to an Array

You can append an item to the end of the array:

```cfml
<cfset ArrayAppend( ThingsILike,  "Tropical Drinks")>
<cfset ArrayAppend( ThingsILike,  42)>
<cfdump var="#ThingsILike#">
```

See how I defined the strings in my array with quotes, and non-strings
without? Each element in the array is an execution zone also, so if you
need ColdFusion to evaluate something, just add it in:

```cfml
<cfset ImportantDates = ["12/26/1975", now() ]>
<cfdump var = "#ImportantDates#">
```

### Displaying the Contents of an Array

You can not use the cfoutput command on an array because complex data
types such as arrays are not displayable as a string. You can loop over
the array, however, and output the strings to the page:

```cfml
<cfset ThingsILike = ["Warm Sandy Beaches", "Tropical Drinks", 42]>
<cfloop array="#ThingsILike#" index="thing">
    <cfoutput>#thing#</cfoutput>
</cfloop>
```

ColdFusion provides many useful functions to work with arrays. You can
search the contents of an array for a value, perform mathematical
functions such Sum or Average, sort the contents of an array, and more.
See the ColdFusion Array function documentation for a full listing:
[https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/array-functions.html](https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/array-functions.html)

## Structs

**Is Simple:** No

Structs, aka Structures, are a collection of data, stored by a key, or
name. Suppose for example, you wanted to store several kinds of fruit
and also whether you like it or not. Structs provide a way to organize
like name/value pairs and let you refer to them as a single collection.

### Struct Creation

```cfml
<cfset FruitBasket = structNew()>
```

### Alternate Method: Struct Creation

```cfml
<cfset FruitBasket = {}>
```

### Adding Items to a Struct: Bracket Notation

```cfml
<cfset FruitBasket = {}>
<cfset FruitBasket["Apple"] = "Like">
<cfset FruitBasket["Banana"] = "Like">
<cfset FruitBasket["Cherry"] = "Dislike">

<cfdump var = "#FruitBasket#">
```

### Adding Items to a Struct Dot Notation

```cfml
<cfset FruitBasket = {}>
<cfset FruitBasket.Apple = "Like">
<cfset FruitBasket.Banana = "Like">
<cfset FruitBasket.Cherry = "Dislike">

<cfdump var = "#FruitBasket#">
```

### Struct Creation and Population in One Statement

```cfml
<cfset fruitBasket = {
    "Apple" = "Like",
    "Banana" = "Like",
    "Cherry" = "Dislike"
}>
<cfdump var = "#FruitBasket#">
```

> **Pro Tip:** There are reasons to use one struct notation over
> another. If you ran some of these examples you would notice that the
> Bracket Notation preserved the case of the keys and the Dot Notation
> did not. Sometimes the preservation of case is important, like when
> passing values to Javascript or other case sensitive languages or
> formats. In the Struct Creation and Population in One Statement
> Example, the case will be preserved as long as they keys are
> surrounded by quotes. If the keys are not quoted, the case will be
> converted to upper case.
>
> Also, the bracket notation allows for a dynamic key reference. This is
> helpful when the name of the struct key will come from a runtime
> operation, such as looping over the struct. Many people find Dot
> Notation easier to read and use it most of the time, except in cases
> where Bracket Notation offers a feature Dot Notation does not.

### Displaying the Contents of a Struct

See how your preference was mapped to the kind of fruit? You can't use
the cfoutput command on structs either because, once again, they aren't
displayable as a string. You can loop over the struct and output the
keys and values to the page:

```cfml
<cfloop collection="#FruitBasket#" item="fruit">
    <cfoutput>I #FruitBasket[fruit]# #fruit#</cfoutput><br>
</cfloop>
```

ColdFusion provides many functions to work with structs. You can search
the contents of struct, make complete and partial copies, retrieve a
list of just the keys, and more. See the ColdFusion Struct function
documentation for a full listing:
[https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/structure-functions.html](https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/structure-functions.html)

## Queries

**Is Simple:** No

Queries are recordsets. Recordsets contain a series of columns with 0 or
more rows. You can think of a query like a single page of a spreadsheet
with the columns across the top and rows down the side.

Most ColdFusion programs interact with databases. Database interaction
takes the form of a query and ColdFusion makes it very easy to work with
the data returned by the database. Databases will be covered more in
more detail later on in the course.

```cfml
<cfquery name="FruitQuery" datasource="fruit">
    SELECT Name, Price
    FROM FruitStore
    WHERE Price < 7
</cfquery>

<cfloop query="FruitQuery">
    #FruitQuery.Name# costs #FruitQuery.Price# <br>
</cfloop>
```

Query Objects have a few special properties. You can use these
properties to get specific information about the data inside the query.

- **queryname.recordcount**: How many rows does this query have?
- **queryname.columnlist**: What columns does this query have?
- **queryname.currentrow**: What row number are we currently on inside
    a cfoutput or cfloop?

ColdFusion provides several useful functions to work with query objects.
You can create your own query objects in ColdFusion without a database
call or even retrieve all the values in a specific column in a list
format. See the ColdFusion Query function documentation for a full
listing:
[https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/query-functions.html](https://helpx.adobe.com/coldfusion/cfml-reference/coldfusion-functions/functions-by-category/query-functions.html)

Also, be sure to review the Accessing and Using Data portion of the
ColdFusion documentation, which explains a number of key concepts and
techniques for working with data, databases and query objects:
[https://helpx.adobe.com/coldfusion/developing-applications/accessing-and-using-data.html](https://helpx.adobe.com/coldfusion/developing-applications/accessing-and-using-data.html)
